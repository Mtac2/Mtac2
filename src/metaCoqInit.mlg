{

(** This module initializes the plugin (parser extension, callbacks, â€¦). *)

(**

   Since Coq 8.5, the following directive must appear to declare plugins. See:

   http://lists.gforge.inria.fr/pipermail/coq-commits/2014-July/012704.html

   Remark: We should add a "How-to write your plugin" section in Coq manual.

*)

}

DECLARE PLUGIN "coq-mtac2.plugin"

{

(** Defines the parser of the proof mode for MetaCoq.

    For the moment, this parser is trivial: an MProof command is
    simply a toplevel Gallina term. We will stay with a trivial parser
    as long as Coq's notations are meeting our needs.

*)

(** Introduce a new parsing rule identifier "vernac:mproof_command".
    This rule is expected to produce a vernac expression. *)
let mproof_mode : Vernacexpr.vernac_expr Pcoq.Entry.t =
  Pcoq.Entry.make "vernac:mproof_command"

(**

   In Coq's parser, the semantic values are typed using the
   module {Genarg} facilities (defined in package "lib").

   We must declare a new type of arguments for MProof instructions
   to type the semantic values produced by our new grammar rule.

   Hence, at the [Genarg] level, we introduce a new type constant
   named "mproof_instr" using the [Genarg.create_arg] function.
   The type of the result [Genarg.genarg_type] is constrained
   to encode the following static property:

   - at the raw level (just after parsing), these semantic values are
   [MetaCoqInstr.mproof_instr] ;

   - TODO it can also be used by Solve Obligations, Hint Extern etc
     but won't work until it has intern and interp functions registered.
     It should also have a subst function registered and raw and glob printers.

*)
let wit_mproof_instr : (MetaCoqInstr.mproof_instr, Util.Empty.t, unit) Genarg.genarg_type =
  Genarg.create_arg "mproof_instr"

(** We introduce a new grammar rule for MProof instructions. The type of
    the semantic values (with_mproof_instr) is specified. *)
let mproof_instr : MetaCoqInstr.mproof_instr Pcoq.Entry.t =
  Pcoq.create_generic_entry2 "mproof_instr" (Genarg.rawwit wit_mproof_instr)

(** We now declare the grammar rule named [mproof_mode] as the entry point
    for proof instructions (installed by [MetaCoqMode] in [ProofGlobal]).

    A grammar rule is defined in three parts (i) the producers ;
    (ii) the effect descriptor needed by STM (iii) the semantic
    value.

    Here, there is only one producer: the non terminal [mproof_instr]
    whose parsing rules are defined at the end of this file. The minus
    sign at the beginning of the rule means that there is no specific
    keyword starting the words derived from this non terminal.

    Finally, we do not produce any semantic value for the moment.

*)
}

VERNAC { mproof_mode } EXTEND MProofInstr
| ![proof] [ mproof_instr(_instr) "." ] =>
  { Vernacextend.classify_as_proofstep } ->
  { fun ~pstate -> MetaCoqInterp.interp_proof_constr ~pstate _instr }
END

{
open G_vernac
}

(** The parsing rule for the non terminal [mproof_instr]. *)
GRAMMAR EXTEND Gram
GLOBAL: mproof_instr mproof_mode;
  mproof_instr :
    [[ c=Pcoq.Constr.term -> { MetaCoqInstr.MetaCoq_constr c } ]];
  mproof_mode:
    [[ p = subprf -> { Vernacexpr.VernacSynPure p }
     | g = OPT toplevel_selector; p = subprf_with_selector -> { Vernacexpr.VernacSynPure (p g) }
     ]]
  ;
END

{
(** Initialize the proof mode MProof for MetaCoq. *)

(** The following identifiers must be globally unique. They are used
    in several global tables to register some callbacks (for instance
    for the parser and the interpreter). *)
let proof_mode_identifier = "MProof"

(** Register the proof mode.

    In our case, we have to set the command entry to "mproof_mode"
    defined in the MetaCoqParser when we enter in proof mode. This
    dynamically change the parser for the proof script instructions.
    See MetaCoqParser to know the syntax of our proof instructions.

    We also reset to the noedit_mode when we quit the MetaCoq proof
    mode.
*)

let _ = Pvernac.register_proof_mode proof_mode_identifier
  (ProofMode {
     command_entry = mproof_mode;
     wit_tactic_expr = wit_mproof_instr;
     tactic_expr_entry = mproof_instr;
   })

(** The following command extends both the parser and the interpreter
   of the Vernacular language so that a new keyword "MProof" is
   recognized and is interpreted as the entry point for a proof
   written in MetaCoq.

   In the following command:

   - On the first line, "MProofCommand" is a new constructor in the
     abstract syntax tree of VernacExpr. It will appear as an
     [extend_name] after the [VernacExtend]. Notice that [extend_name]
     is a pair of a string (which is "MProofCommand" here) and an
     integer which represents the index of the interpretation rule
     (here 0).

   - On the second line, [ "MProof" ] is the right hand side of the
     unique grammar rule for the non terminal MProofCommand.

   - On the third line, there are two classifiers that influence
     the interpretation of this command.

     "VtProofMode" classifies the command as a proof mode introducer.
     The "proof_mode_identifier" is used as a key in
     Vernac_classifier.classifiers.

     "VtNow" indicates that the interpretation of this command cannot
     be done in the background asynchronously. Indeed, changing the
     proof mode has an effect on the parsing and the interpretation
     of the subsequent commands.

    - On the fourth line, the interpretation function is given. This
     interpretation function is registered in Vernacinterp and called
     in Vernacentries.interp.
*)

}

VERNAC COMMAND EXTEND MProofCommand
| [ "MProof" ]
  => { (match Pvernac.lookup_proof_mode proof_mode_identifier with
       | None ->
         CErrors.user_err Pp.(str (Format.sprintf "No proof mode named \"%s\"."
            proof_mode_identifier))
       | Some proof_mode -> Vernacextend.VtProofMode proof_mode)
     }
  -> { MetaCoqInterp.interp_mproof_command () }
END
